/* -*- ld-script -*- */

/* Memory layout:
 * - at the beginning, set of bootstrap values resides ("registers")
 * - Then, data and code
 */

/* Part of the source is raw binary. */
TARGET(binary)
INPUT(raw)
TARGET(elf32-i386)
OUTPUT_FORMAT(elf32-i386)
OUTPUT(test)


ENTRY(_start)

PHDRS
{
  all PT_LOAD;
}

SECTIONS
{
 . = 0x10000;
 . = . + SIZEOF_HEADERS;
 .registers : { *(.data.registers) } : all
 .text : { *(.text) *(.rodata) *.o(.data)} : all
 .data ALIGN(0x200) : { raw(.data); } : all
 .heap : {*(.bss.pages .bss.heap .bss.words .bss) } : all
			     
/DISCARD/ : {*(.iplt .eh_frame .rel.dyn .igot.plt .comment .debug_*)}
}
